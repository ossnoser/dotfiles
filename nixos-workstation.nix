# Edit this configuration file to define what should be installed on
# your system.  Help is available in the configuration.nix(5) man page
# and in the NixOS manual (accessible by running ‘nixos-help’).

{ config, pkgs, specialArgs, ... }:

{
  imports = [
    ./nixos-base.nix
  ];

  # Use latest kernel for workstations
  boot.kernelPackages = pkgs.linuxPackages_latest;

  #This is to allow maestral (dropbox sync client) to work with the nasty Neuromod dropbox mess
  boot.kernel.sysctl."fs.inotify.max_user_watches" = 1048576;

  # Keyboard and mouse
  services.xserver.xkb = {
    layout = "ie";
    variant = "";
  };

  # Audio
  services.pulseaudio.enable = false;
  security.rtkit.enable = true;
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
    jack.enable = true;
  };

  services.blueman.enable = true;
  services.printing.enable = true;

  # Laptop bits
  services.thermald.enable = true;
  # This conflicts with services.power-profiles.daemon.enable... which appears to be true by default
  # services.tlp.enable = true;

  # Enable OpenGL
  # ... on unstable
  hardware.graphics = {
    enable = true;
  };
  # ... on 24.05
  # hardware.opengl = {
  #   enable = true;
  #   # driSupport = true;
  #   # driSupport32Bit = true;
  # };

  # Display manager etc.
  services.xserver.enable = true;
  services.displayManager.gdm.enable = true;
  services.gnome.gnome-keyring.enable = true;


  # Desktop environments
  # services.desktopManager.gnome.enable = true;

  # Compatibility shim for dynamic binaries....
  # See https://github.com/mic92/envfs
  # services.envfs.enable = true;

  # Dev tools...
  services.udev.packages = [
    pkgs.nrf-udev
    pkgs.openocd
    pkgs.segger-jlink
  ];

  # segger-jlink (needed for its udev rules above) is unfree and pulls in the
  # insecure segger-jlink-qt4-810. allowUnfree is set in nixos-base.nix; the
  # remaining overrides must be on the NixOS system nixpkgs config too.
  nixpkgs.config.permittedInsecurePackages = [ "segger-jlink-qt4-952" ];
  nixpkgs.config.segger-jlink.acceptLicense = true;

  # Required to install sway via home-manager
  # ... but we're installing via nixos
  #security.polkit.enable = true;

  programs.sway = {
    enable = true;
    wrapperFeatures.gtk = true;
    extraOptions = [
      "--unsupported-gpu"
    ];
    extraSessionCommands = ''
    # SDL
    export SDL_VIDEODRIVER=wayland
    # QT (needs qt5.qtwayland in systemPackages):
    export QT_QPA_PLATFORM=wayland-egl
    export QT_WAYLAND_DISABLE_WINDOWDECORATION="1"
    # Fix for some Java AWT applications (e.g. Android Studio),
    # use this if they aren't displayed properly:
    export _JAVA_AWT_WM_NONREPARENTING=1
    '';
  };

  programs.hyprland = {
    enable = true;
    withUWSM = true;
  };
  programs.hyprlock.enable = true;


  # List packages installed in system profile. To search, run:
  # $ nix search wget
  # `--alternate-editor=` (empty value) makes emacsclient start `emacs --daemon`
  # on demand instead of failing with "can't find socket"; there is no
  # services.emacs unit. Written as the long `=` form so consumers that split
  # $EDITOR on whitespace without shell quoting still pass an empty value.
  environment.variables.EDITOR = pkgs.lib.mkForce "emacsclient --alternate-editor= -nw";
  # No "-n": git resolves GIT_EDITOR -> core.editor -> VISUAL -> EDITOR, so
  # VISUAL (not EDITOR) is what `git commit` actually runs on this host, and
  # -n (--no-wait) would make emacsclient return before the commit buffer is
  # edited, aborting with an empty message. See design/log/2026-09-03-bootstrap-standalone-emacs-config.org.
  environment.variables.VISUAL = "emacsclient --alternate-editor= -c";

  environment.systemPackages = with pkgs; [
    waybar
    # Install GPU accelerated terminals at os level
    kitty
    ghostty
    # ... kitty is the default terminal; foot is still installed via home-manager for the pi term extension
    # Install system python at OS level...
    # ... this keeps home-manager config compatible with Arch linux package management
    python3
    pipenv
    # Temporary workaround for https://github.com/NixOS/nixpkgs/issues/544083 - July 2026
    (poetry.overridePythonAttrs (old: {
      doCheck = false;
    }))
    # poetry
    # Virtual machines etc.
    qemu SDL2

    qt5.qtwayland
    # segger-jlink
    openocd
  ];
}
