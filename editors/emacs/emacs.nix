{ config, pkgs, inputs, ... }:

let
  # Spacemacs alone is held at Emacs 30.2 via the rev-pinned `nixpkgs-spacemacs`
  # flake input -- 31.1 breaks it, and nixpkgs no longer carries an `emacs30-*`
  # attribute. PATH `emacs` and every other config track the main nixpkgs.
  pkgsSpacemacs = import inputs.nixpkgs-spacemacs {
    inherit (pkgs.stdenv.hostPlatform) system;
    config.allowUnfree = true;
  };

  # Pinned 30.2 Emacs used only by the `semacs` alias below. Not on PATH.
  spacemacsEmacs = (pkgsSpacemacs.emacsPackagesFor pkgsSpacemacs.emacs).emacsWithPackages
    (epkgs: [ epkgs.vterm ]);

  commonSessionVariables = {
    #Use xdg-config layout for spacemacs
    SPACEMACSDIR = "${config.xdg.configHome}/spacemacs";
    #emacs/org need to find plantuml jar rather than binary
    PLANTUML_JAR = "${pkgs.plantuml}/lib/plantuml.jar";
  };
in {
  #User environment
  home.sessionVariables = commonSessionVariables;
  #... and environment.d for gdm, kdm etc. that don't source user profile
  systemd.user.sessionVariables = commonSessionVariables;

  programs.pandoc.enable = true;
  programs.texlive.enable = true;

  #N.B. See dev.nix for font installation...
  home.packages = with pkgs; [
    (aspellWithDicts (dicts: with dicts; [en en-computers en-science ga]))
    ripgrep
    gsettings-desktop-schemas
    libvterm-neovim
    # org export
    zip #for ODT export
    # Other...
    libtool
    cmake
    gnumake
    gcc
    # dirvish file previews (`fd' comes from programs.fd in shell/shell.nix,
    # 7z from p7zip). Each missing program makes dirvish warn on first use.
    vips # vipsthumbnail -- images
    ffmpegthumbnailer # video thumbnails
    mediainfo # audio/video metadata
    poppler-utils # pdftoppm -- pdf
    imagemagick # magick -- fonts
    # charts
    plantuml
    # plantuml-c4
    # jdk21
    graphviz
    mermaid-cli
    # Layer dependencies.
    # TODO: install these in project flakes instead maybe?
    #       ... though the layers are enabled in global config ...
    # ... bash
    bash-language-server
    # ... c/c++
    # FIXME: Causing an installation error as of 26/08/2025
    # cmake-language-server
    clang-tools
    # ... python
    pyright
    black
    # ... clojure
    clj-kondo
    joker
    clojure-lsp
    # ...
  ];

  home.file.".local/bin/md2org".source=./bin/md2org;
  home.file.".local/bin/org2md".source=./bin/org2md;

  # Emacs and dependencies
  programs.emacs = {
    enable = true;
    extraPackages = (epkgs: [ epkgs.vterm ]);
  };

  # Minimal literate config -- the *default* configuration, so bare `emacs`
  # (and `emacsclient --alternate-editor=`) picks it up through the normal XDG
  # startup search, with no alias and no --init-dir.
  #
  # Same out-of-store pattern as Corgi: the three sources are editable in place
  # and the containing directory stays writable, so elpaca clones, the tangled
  # config.el, and eln caches live in ~/.config/emacs, not in the repo.
  xdg.configFile."emacs/early-init.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/minimal/early-init.el";
  xdg.configFile."emacs/init.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/minimal/init.el";
  xdg.configFile."emacs/config.org".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/minimal/config.org";


  # Alternate distributions

  # ... Spacemacs

  # Launched via the `semacs` alias on the pinned 30.2 build; deliberately not
  # at ~/.config/emacs, which now holds the default literate config below.
  home.file."${config.xdg.configHome}/emacs-spacemacs" = {
    recursive = true;
    #Use this variant to pin a specific commit
    # source = pkgs.fetchFromGitHub {
    #   owner = "syl20bnr";
    #   repo = "spacemacs";
    #   rev = "e4b20f797d9e7a03d9a5603942c4a51ea19047b2";
    #   #N.B. If updating rev above, new sha256 will be reported when trying to swap this flake in, and can be pasted here
    #   sha256 = "OdZuOmxDYvvsCnu9TcogCeB0agCq8o20/YPCmUSwYPw=";
    # };
    #... or this variant to track a branch
    source = builtins.fetchGit {
      url = "https://github.com/syl20bnr/spacemacs";
      ref = "develop";
    };
  };
  # Do this to have a symlinked read-only version
  # home.file."${config.xdg.configHome}/spacemacs".source = .config/spacemacs;
  # ... or this to keep it editable in-place, rather than have to 'home-manager switch ...' after each edit
  home.file."${config.xdg.configHome}/spacemacs".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/spacemacs";


  # ... Corgi emacs... a clojure-focused minimal config with spacemacs-like keybindings
  # See https://github.com/corgi-emacs/corgi

  # Keep the config files editable in place, but leave the containing directory
  # writable so Emacs runtime state does not end up in the dotfiles repository.
  xdg.configFile."emacs-corgi/bootstrap.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/corgi/bootstrap.el";
  xdg.configFile."emacs-corgi/early-init.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/corgi/early-init.el";
  xdg.configFile."emacs-corgi/init.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/corgi/init.el";
  xdg.configFile."emacs-corgi/user-keys.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/corgi/user-keys.el";
  xdg.configFile."emacs-corgi/user-signals.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/dotfiles/editors/emacs/corgi/user-signals.el";


  # Bare `emacs` loads the default / literate config
  home.shellAliases = {
    cemacs = "emacs --init-dir ~/.config/emacs-corgi";
    spacemacs = "${spacemacsEmacs}/bin/emacs --init-dir ${config.xdg.configHome}/emacs-spacemacs";
  };
}
